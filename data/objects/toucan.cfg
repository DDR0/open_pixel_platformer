{
id: "toucan",
zorder: "@include data/zorder.cfg:foreground",

editor_info: {
	category: "actors",
	help: "A nicely-animated tropical bird with a large beak.",
},

properties: {
	//Choose a random animation. If a list is passed in, do not choose any animations in the list. This list defaults to containing the current animation.
	choose_next_animation: "overload(
		def() choose_next_animation([animation]),
		def(excluding) [
				animation(choose(potential_animations)),
				if(1d8 = 1, set(facing, -facing)),
			]
			where potential_animations = filter(
				available_animations+['stand']*4, //Make standing more likely. The bird is a bit busy otherwise.
				not (value in excluding or value = 'fly')) //Exclude the flying animation. Perhaps move it to a separate object?
	)"
},

on_create: "animation('hop')",

on_end_stand_anim: "choose_next_animation([])",

on_end_squawk_anim: "choose_next_animation()",

on_end_look_up_anim: "choose_next_animation()",

on_end_look_down_anim: "choose_next_animation()",

on_end_hop_anim: "[
	add(x, 40*facing),
	choose_next_animation(),
]",

on_end_fly_anim: "animation('fly')",

animation: [
	{
		"@base": true,
		x:0, y:0, w:64, h:64,
		image: "actors/toucan.png",
		duration: 4,
		feet_x: 21,
		feet_y: 31,
	},
	
	{
		id: "stand", //Reuses the first frame of the squawk animation.
		image: "actors/toucan.png",
		duration: 40,
	},
	
	{
		id: "squawk",
		image: "actors/toucan.png",
		y: "@eval 0*64",
		frames: 3,
		duration: 5,
		reverse: true,
	},
	
	{
		id: "look_up",
		image: "actors/toucan.png",
		y: "@eval 1*64",
		frames: 6,
		duration: 16,
	},
	
	{
		id: "look_down",
		image: "actors/toucan.png",
		y: "@eval 2*64",
		frames: 6,
		duration: 16,
	},
	
	{
		id: "hop",
		image: "actors/toucan.png",
		y: "@eval 3*64",
		frames: 5,
	},
	
	{
		id: "fly",
		image: "actors/toucan.png",
		y: "@eval 4*64",
		frames: 4,
	},
],
}